plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
    id "com.github.johnrengelman.shadow" version "7.1.1"
}

group = 'io.github.eone666'
version = 'preview'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:1.8.10"

    implementation "com.github.elbekD:kt-telegram-bot:2.2.0"
    implementation "com.github.shynixn.mccoroutine:mccoroutine-bukkit-api:2.13.0"
    implementation "com.github.shynixn.mccoroutine:mccoroutine-bukkit-core:2.13.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"
}

tasks.jar { enabled = false }
artifacts.archives(tasks.shadowJar)

tasks.shadowJar {
    archiveFileName.set("${rootProject.name}-${version}.jar")
    manifest {
        attributes["Main-Class"] = "io.github.eone666.telegramnotifier.TelegramNotifier"
    }

    exclude("org/intellij/**")
    exclude("org/jetbrains/**")
    exclude("org/slf4j/**")
    exclude("org/eclipse/**")
    exclude("kotlin/**")

    exclude("DebugProbesKt.bin")
    exclude("jetty-dir.css")
    exclude("META-INF/**")

    minimize()
}


def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

kotlin {
    jvmToolchain(targetJavaVersion)
}